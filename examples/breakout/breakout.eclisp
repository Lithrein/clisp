(%:include "macros.eclisph")

(%:include <stdbool.h> <stdio.h> <stdlib.h> <time.h>)
(%:include <SDL2/SDL.h>)

(%:define WIDTH  640)
(%:define HEIGHT 480)

(%:define PADDLE_LEN       100)
(%:define PADDLE_THICKNESS   5)
(%:define PADDLE_Y         465)

(%:define BROWS    8)
(%:define BCOLS   16)
(%:define BWIDTH  40)
(%:define BHEIGHT 20)
(%:define BMARGIN  2)

(%:define DELTA_X 10)

(macro min (a b) `(?: (< ,a ,b) ,a ,b))
(macro max (a b) `(?: (> ,a ,b) ,a ,b))

(macro defstruct (name &body rest)
  `(prog*
     (def ,name (typedef struct ,name))
     (def (struct ,name ,@rest))))

(macro SDL_SetRenderDrawColorStruct (renderer color)
 `(SDL_SetRenderDrawColor ,renderer
                         (. ,color r) (. ,color g) (. ,color b) (. ,color a)))

(defstruct Point (x y int))

(defstruct Ball
  (pos SDL_Rect)
  (vit Point))

(def game_loop (-> int (renderer (ptr SDL_Renderer)))
  (def screen SDL_Rect '(0 0 WIDTH HEIGHT))

  (def quit (bool) false)
  (def left int    (/ (- WIDTH PADDLE_LEN) 2))
  (def ball Ball  `((,(+ left (/ PADDLE_LEN 2)) ,(- PADDLE_Y 5)
                      PADDLE_THICKNESS PADDLE_THICKNESS)
                   (0 0)))
  (def red   SDL_Color '(255 100 100 0))
  (def green SDL_Color '(100 255 100 0))
  (def blue  SDL_Color '(100 100 255 0))
  (def gray  SDL_Color '(150 150 150 0))
  (def white SDL_Color '(255 255 255 0))
  (def black SDL_Color '(  0   0   0 0))
  (def active int 0)

  (def board (array BROWS (array BCOLS int)) '(0))
  (for (def i int 0) (< i BROWS) ++i
       (for (def j int 0) (< j BCOLS) ++j
            (= (aref board i j) 1)))

  (until quit
    (def event (SDL_Event))
    (while (SDL_PollEvent (& event))
      (switch (. event type)
              (default (break))
              (SDL_QUIT
                (= quit true)
                (break))
              (SDL_KEYDOWN
                 (switch (. event key keysym sym)
                         (default (break))
                         (SDLK_q
                           (= quit true)
                           (break))
                         (SDLK_x
                           (*= (. ball vit x) 2)
                           (*= (. ball vit y) 2)
                           (break))
                         (SDLK_c
                           (/= (. ball vit x) 2)
                           (/= (. ball vit y) 2)
                           (break))
                         (SDLK_SPACE
                           (= (. ball vit x) -1)
                           (= (. ball vit y) -3)
                           (= active 1)
                           (break))
                         (SDLK_LEFT
                           (= left (max 0 (- left DELTA_X)))
                           (unless active
                             (= (. ball pos x) (+ left (/ PADDLE_LEN 2))))
                           (break))
                         (SDLK_RIGHT
                           (= left (min (- WIDTH PADDLE_LEN) (+ left DELTA_X)))
                           (unless active
                             (= (. ball pos x) (+ left (/ PADDLE_LEN 2))))
                           (break))))))
      (SDL_Delay 10)

      ;; handle a collision with a wall
      (if (< (. ball pos y) 0)
        (*= (. ball vit y) -1))
      (if (< (. ball pos x) 0)
        (*= (. ball vit x) -1))
      (if (> (. ball pos x) WIDTH)
        (*= (. ball vit x) -1))

      ;; The bounce on the paddle
      (when (&& active
            (<= left (. ball pos x) (+ left PADDLE_LEN))
            (< PADDLE_Y (. ball pos y)))
        (*= (. ball vit y) -1)
        (= (. ball vit x) (?: (< (. ball pos x) (+ (/ PADDLE_LEN 2) left))
                              (- (abs (. ball vit x)))
                              (abs (. ball vit x)))))

      ;; when you die, repop
      (when (> (. ball pos y) HEIGHT)
        (= active 0)
        (= left (- (/ WIDTH 2) PADDLE_LEN))
        (= (. ball pos x) (+ left (/ PADDLE_LEN 2)))
        (= (. ball pos y) (- PADDLE_Y 5))
        (= (. ball vit x) (. ball vit y) 0))


      ;; handle a collision with a brick
      (when (<= 0 (. ball pos y) (* BROWS BHEIGHT))
        (def i int (min (- BROWS 1) (/ (. ball pos y) BHEIGHT)))
        (def j int (min (- BCOLS 1) (/ (. ball pos x) BWIDTH)))
        (when (aref board i j)
          (= (aref board i j) 0)
          (= (. ball vit y) (- (. ball vit y)))))

      ;; update the position of the ball
      (+= (. ball pos x) (. ball vit x))
      (+= (. ball pos y) (. ball vit y))

      ;; clear the screen
      (SDL_SetRenderDrawColorStruct renderer black)
      (SDL_RenderFillRect renderer (& screen))

      ;; draw the screen
      (for (def i int 0) (< i BROWS) ++i
           (for (def j int 0) (< j BCOLS) ++j
                (unless (aref board i j) (continue))

                (def obrick SDL_Rect `(,(* j BWIDTH) ,(* i BHEIGHT) ,(/ WIDTH BCOLS) BHEIGHT))
                (def ibrick SDL_Rect `(,(+ (* j BWIDTH) BMARGIN) ,(+ (* i BHEIGHT) BMARGIN)
                                       ,(- (/ WIDTH BCOLS) (* 2 BMARGIN)) ,(- BHEIGHT (* 2 BMARGIN))))
                (SDL_SetRenderDrawColorStruct renderer gray)
                (SDL_RenderFillRect renderer (& obrick))
                (switch (% (+ i j) 3)
                 (1
                   (SDL_SetRenderDrawColorStruct renderer red)
                   (break))
                 (2
                   (SDL_SetRenderDrawColorStruct renderer green)
                   (break))
                 (default
                   (SDL_SetRenderDrawColorStruct renderer blue)
                   (break)))
                (SDL_RenderFillRect renderer (& ibrick)))

           (def paddle (SDL_Rect) `(,left PADDLE_Y PADDLE_LEN PADDLE_THICKNESS))
           (SDL_SetRenderDrawColorStruct renderer white)
           (SDL_RenderFillRect renderer (& paddle))
           (SDL_SetRenderDrawColorStruct renderer blue)
           (SDL_RenderFillRect renderer (& (. ball pos))))

      ;; refresh time
      (SDL_RenderPresent renderer))
  (return 1))

(def main (-> int void)
  (srand (time NULL))

  (with-sdl2-init (SDL_INIT_VIDEO)
    (with-sdl2-window
      (window "Wallbreaker"
              SDL_WINDOWPOS_CENTERED SDL_WINDOWPOS_CENTERED
              WIDTH HEIGHT
              SDL_WINDOW_SHOWN)
      (with-sdl2-renderer
        (renderer window -1 SDL_RENDERER_ACCELERATED)
        (game_loop renderer)
        (return EXIT_SUCCESS)))))

